//athletes quests

//Procedural components
const FETCH_TYPE = 1
const CAPTURE_TYPE = 2
const TRAVEL_TYPE = 3
const HATCH_TYPE = 4


script(global) RyuAthletesDailyQuest  {
    lock
    faceplayer
     if (flag(FLAG_FINISHED_ATHLETE_DAILY) == TRUE) { //quest done
        msgbox("Want to join me for some\nexercise?")
        release
        end
    }

    checkfactionstanding(FACTION_ATHLETES, 200)

    if (var(VAR_RESULT) == TRUE) {
        msgbox("I think we may be able to\nbreak the triathlon time record\lsoon!")
        release
        end
    }

    if (flag(FLAG_DAILY_QUEST_ACTIVE) == TRUE && var(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION) == FACTION_ATHLETES) { //currently doing a daily quest for this faction
        switch (var(VAR_RYU_DAILY_QUEST_TYPE)) {
            case FETCH_TYPE:
                checkitem(VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                if (var(VAR_RESULT) == TRUE) {
                    removeitem(VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                    goto(CommonAthletesRewardScript)
                }
                else {
                    buffernumberstring(0, VAR_RYU_DAILY_QUEST_DATA)
                    bufferitemnameplural(1, VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                    msgbox("Hey, I'm in a hurry.\pDo you have the {STR_VAR_1} {STR_VAR_2} yet?")
                    release
                    end
                }
            case CAPTURE_TYPE:
                end
            case TRAVEL_TYPE:
                if (var(VAR_RYU_DAILY_QUEST_DATA) == 4000) {
                    msgbox(format("Don't you feel good after all that exercise?"))
                    goto(CommonAthletesRewardScript)
                }
                else {
                    buffermapname(0, VAR_RYU_DAILY_QUEST_TARGET)
                    msgbox(format("Don't quit on us now! Run to {STR_VAR_1} and FEEL THE BURN!!"))
                    release
                    end
                }
            case HATCH_TYPE:
                checkspecies(VAR_RYU_DAILY_QUEST_TARGET)
                if (var(VAR_TEMP_F) < 6) {
                    bufferspeciesname(0, VAR_RYU_DAILY_QUEST_TARGET)
                    msgbox(format("I see, so it was a {STR_VAR_1}! Well, thanks for letting me know, take care of it!"))
                    goto(CommonAthletesRewardScript)
                    release
                    end
                }
                else {
                    msgbox(format("Have you hatched that egg yet?"))
                    release
                    end
                }
        }

    }
    elif (flag(FLAG_DAILY_QUEST_ACTIVE) == TRUE && var(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION) != FACTION_ATHLETES) {//currently doing a daily for another faction
        setvar(VAR_TEMP_9, FACTION_ATHLETES)
        callnative(RyuSummonOriginalNPCscript)
        end
    }
    
    setvar(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION, FACTION_ATHLETES)
    random(3)//increase this by the number of quests you add. 
    switch(var(VAR_RESULT)) { //Put new quests here.
        case 0:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, FETCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, ITEM_PROTEIN)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 2)
            msgbox("We're almost out of protein powder.\pCan you bring us 2 bottles of Protein?\nIt's needed for all of us who are\nactively working out!")
        case 1:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, FETCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, ITEM_EVPRINT)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 10)
            msgbox("We need to do some imprinting at\nthe Training Center.\pCan you bring us ten imprints?\nThis will save us a lot of time.")
        case 2:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, TRAVEL_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, MAP_ROUTE111)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 15)
            msgbox(format("{PLAYER}, are you up for a challenge? Of course you are! I want you to run to Route 111 and back. Enjoy the exercise!"))
        case 3:
            random(4)
            setvar(VAR_RYU_DAILY_QUEST_TYPE, HATCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 0)
            callnative(RyuSetupEggHatchQuest)
            giveegg(VAR_RYU_DAILY_QUEST_TARGET)
            msgbox(format("I came across this egg during my morning run. I'd like to know what it is, but I don't have time to hatch it myself. Could you do it for me?"))
            closemessage
            msgbox(FactionsReceivedEggMsg)
    }
    setflag(FLAG_DAILY_QUEST_ACTIVE)
    closemessage
    release
    end
}

script CommonAthletesRewardScript {
    msgbox("Continue to be the best you\ncan be!")
    setflag(FLAG_FINISHED_ATHLETE_DAILY)
    changefactionstanding(FACTION_ATHLETES, 25, FALSE)
    addmoney(1750)
    buffernumberstring(0, 1750)
    goto(CommonDailyQuestEnd)
}