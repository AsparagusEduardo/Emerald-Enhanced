//This is the scripts file for the procedural generation of mini-quests for factions

//Procedural components
const FETCH_TYPE = 1
const CAPTURE_TYPE = 2
const TRAVEL_TYPE = 3
const HATCH_TYPE = 4

script(global) RyuNaturalistsDailyQuest  {
    lock
    faceplayer
    if (flag(FLAG_FINISHED_NATURALIST_DAILY) == TRUE) { //quest done
        msgbox("I don't have anything else\nfor you to do right now, sorry!")
        release
        end
    }

    checkfactionstanding(FACTION_NATURALISTS, 200)

    if (var(VAR_RESULT) == TRUE) {
        msgbox("Hi {PLAYER}! The Naturalists are doing quite\nwell thanks to your help.")
        release
        end
    }

    if ((flag(FLAG_DAILY_QUEST_ACTIVE) == TRUE) && (var(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION) == FACTION_NATURALISTS)) { //currently doing a daily quest for this faction
        switch (var(VAR_RYU_DAILY_QUEST_TYPE)) {
            case FETCH_TYPE:
                checkitem(VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                if (var(VAR_RESULT) == TRUE) {
                    removeitem(VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                    goto(CommonNaturalistRewardScript)
                }
                else {
                    buffernumberstring(0, VAR_RYU_DAILY_QUEST_DATA)
                    bufferitemnameplural(1, VAR_RYU_DAILY_QUEST_TARGET, VAR_RYU_DAILY_QUEST_DATA)
                    msgbox("We still need {STR_VAR_1} {STR_VAR_2}.")
                    release
                    end
                }
            case CAPTURE_TYPE:
                end
            case TRAVEL_TYPE:
                if (var(VAR_RYU_DAILY_QUEST_DATA) == 4000) {
                    msgbox(format("That sounds like a great place to camp, thanks for scouting it out for me!"))
                    goto(CommonNaturalistRewardScript)
                }
                else
                {
                    buffermapname(0, VAR_RYU_DAILY_QUEST_TARGET)
                    msgbox(format("Does {STR_VAR_1} have a good place to camp?"))
                    release
                    end
                }
            case HATCH_TYPE:
                checkspecies(VAR_RYU_DAILY_QUEST_TARGET)
                if (var(VAR_TEMP_F) < 6) {
                    bufferspeciesname(0, VAR_RYU_DAILY_QUEST_TARGET)
                    msgbox(format("Ah, so it was a {STR_VAR_1}. It would make an excellent camping companion."))
                    goto(CommonNaturalistRewardScript)
                    release
                    end
                }
                else
                {
                    msgbox(format("Have you hatched that egg I gave you yet?"))
                    release
                    end
                }
        }

    }
    elif (flag(FLAG_DAILY_QUEST_ACTIVE) == TRUE && var(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION) != FACTION_NATURALISTS) {//currently doing a daily for another faction
        setvar(VAR_TEMP_9, FACTION_NATURALISTS)
        callnative(RyuSummonOriginalNPCscript)
        end
    }

    setvar(VAR_RYU_DAILY_QUEST_ASSIGNEE_FACTION, FACTION_NATURALISTS)
    random(3)//increase this by the number of quests you add. 
    switch(var(VAR_RESULT)) { //Put new quests here.
        case 0:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, FETCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, ITEM_SITRUS_BERRY)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 3)
            msgbox("Hi, {PLAYER}!\nCan you get some Sitrus Berries\lfor me?\pThree of them should do.")
        case 1:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, FETCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, ITEM_PECHA_BERRY)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 2)
            msgbox("Hi, {PLAYER}!\nCan you get some Pecha Berries\lfor me?\pTwo of them should do.")
        case 2:
            random(4)
            setvar(VAR_RYU_DAILY_QUEST_TYPE, HATCH_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 15)
            callnative(RyuSetupEggHatchQuest)
            giveegg(VAR_RYU_DAILY_QUEST_TARGET)
            msgbox(format("Hey, I found this egg just laying around in the woods. Could you hatch it, so we know what it is? Thanks."))
            closemessage
            msgbox(FactionsReceivedEggMsg)
        case 3:
            setvar(VAR_RYU_DAILY_QUEST_TYPE, TRAVEL_TYPE)
            setvar(VAR_RYU_DAILY_QUEST_TARGET, MAP_ROUTE103)
            setvar(VAR_RYU_DAILY_QUEST_DATA, 0)
            msgbox(format("{PLAYER}, can you go check out Route 103? I want to know specifically if there's a good place to camp there or not."))
    }
    setflag(FLAG_DAILY_QUEST_ACTIVE)
    closemessage
    release
    end
}

//Common reward scripts for dailies

script CommonNaturalistRewardScript {
    msgbox("Thanks for getting the stuff!")
    setflag(FLAG_FINISHED_NATURALIST_DAILY)
    changefactionstanding(FACTION_NATURALISTS, 25, FALSE)
    buffernumberstring(0, 5000)
    addmoney(5000)
    goto(CommonDailyQuestEnd)
}

script CommonDailyQuestEnd {
    clearflag(FLAG_DAILY_QUEST_ACTIVE)
    msgbox("You earned Â¥{STR_VAR_1}.")
    closemessage
    release
    end
}

text RDBNAT1 {
    "VAR RESULT is {STR_VAR_1}\n QUEST DATA is {STR_VAR_2}"
}
